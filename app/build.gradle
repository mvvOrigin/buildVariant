apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.original.buildvariant"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            ext.enableCrashlytics = false
            buildConfigField "String", "API_SUFFIX", rootProject.endpoints.suffix.test
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            ext.enableCrashlytics = true
            buildConfigField "String", "API_SUFFIX", rootProject.endpoints.suffix.prod
        }
    }
    productFlavors {
        disney {
            applicationId "com.disney.android"
            //signingConfig.disney
            buildConfigField "String", "API_ENDPOINT", rootProject.endpoints.api.disney
            resValue "string", "youtube_key", rootProject.keys.youtube.disney
            versionCode 102
            versionName "1.0.2"
        }
        marvel {
            applicationId "com.marvel.android"
            //signingConfig.marvel
            buildConfigField "String", "API_ENDPOINT", rootProject.endpoints.api.marvel
            resValue "string", "youtube_key", rootProject.keys.youtube.marvel
            versionCode 136
            versionName "1.3.6"
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    testCompile 'junit:junit:4.12'
}
